(ns kotoba.raytrace.bvh-test
  "Parity port of kami-rt's bvh.rs `#[cfg(test)] mod tests`."
  (:require [clojure.test :refer [deftest is]]
            [kotoba.raytrace.vec3 :as vec3]
            [kotoba.raytrace.bvh :as bvh]))

(defn- quad-grid
  "n x n unit quads on the z=0 plane, two triangles each, spaced by 1 on x/y."
  [n]
  (vec
   (for [gy (range n)
         gx (range n)
         :let [x (double gx) y (double gy)
               a (vec3/v3 x y 0.0)
               b (vec3/v3 (+ x 0.9) y 0.0)
               c (vec3/v3 x (+ y 0.9) 0.0)
               d (vec3/v3 (+ x 0.9) (+ y 0.9) 0.0)]
         [v0 v1 v2] [[a b c] [b d c]]]
     {:v0 v0 :v1 v1 :v2 v2})))

(defn- with-ids [tris]
  (vec (map-indexed (fn [i t] (assoc t :id i)) tris)))

(deftest empty-bvh-is-safe
  (let [bvh (bvh/build [])]
    (is (= 1 (bvh/node-count bvh)))
    (is (nil? (bvh/traverse bvh (vec3/v3 0.0 0.0 0.0) (vec3/scale (vec3/v3 0.0 0.0 1.0) -1.0))))))

(deftest hits-a-single-triangle
  (let [tris [{:v0 (vec3/v3 -1.0 -1.0 -5.0)
               :v1 (vec3/v3 1.0 -1.0 -5.0)
               :v2 (vec3/v3 0.0 1.0 -5.0)
               :id 42}]
        bvh (bvh/build tris)
        hit (bvh/traverse bvh (vec3/v3 0.0 0.0 0.0) (vec3/v3 0.0 0.0 -1.0))]
    (is (some? hit))
    (is (= 42 (:tri-id hit)))
    (is (< (Math/abs (- (:t hit) 5.0)) 1e-4))))

(deftest misses-when-off-axis
  (let [tris [{:v0 (vec3/v3 -1.0 -1.0 -5.0)
               :v1 (vec3/v3 1.0 -1.0 -5.0)
               :v2 (vec3/v3 0.0 1.0 -5.0)
               :id 0}]
        bvh (bvh/build tris)]
    (is (nil? (bvh/traverse bvh (vec3/v3 50.0 50.0 0.0) (vec3/v3 0.0 0.0 -1.0))))))

(deftest bvh-matches-brute-force-over-a-grid
  (let [tris (with-ids (quad-grid 8)) ; 128 triangles
        bvh (bvh/build tris)]
    (is (> (bvh/node-count bvh) 1) "internal nodes must exist (not one big leaf)")
    (doseq [gy (range 8)
            gx (range 8)]
      (let [origin (vec3/v3 (+ gx 0.3) (+ gy 0.3) 5.0)
            dir (vec3/v3 0.0 0.0 -1.0)
            bvh-hit (bvh/traverse bvh origin dir)
            brute (reduce
                   (fn [best t]
                     (if-let [d (bvh/tri-intersect t origin dir)]
                       (if (or (nil? best) (< d (:t best)))
                         {:t d :tri-id (:id t)}
                         best)
                       best))
                   nil tris)]
        (cond
          (and bvh-hit brute)
          (do (is (< (Math/abs (- (:t bvh-hit) (:t brute))) 1e-4)
                  (str "t mismatch at (" gx "," gy ")"))
              (is (= (:tri-id bvh-hit) (:tri-id brute))
                  (str "id mismatch at (" gx "," gy ")")))

          (and (nil? bvh-hit) (nil? brute)) (is true)

          :else (is false (str "BVH/brute-force disagree at (" gx "," gy ")")))))))

(deftest morton-is-monotone-on-a-diagonal
  (let [a (bvh/morton3 (vec3/v3 0.1 0.1 0.1))
        b (bvh/morton3 (vec3/v3 0.5 0.5 0.5))
        c (bvh/morton3 (vec3/v3 0.9 0.9 0.9))]
    (is (< a b))
    (is (< b c))))
